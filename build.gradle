plugins {
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'java'
    id 'application'
    id 'checkstyle'
    // id "com.diffplug.gradle.spotless" version "3.18.0"
    id 'com.github.spotbugs' version '1.6.9'
    // id 'pmd'
    id 'jacoco'
    id "org.ec4j.editorconfig" version "0.0.3"
}

apply plugin: 'application'
// apply plugin: 'com.github.johnrengelman.shadow'

group 'edu.cornell.cs.apl'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

configurations {
    cup
    jflex
}

repositories {
    jcenter()
}

application {
    mainClassName = 'edu.cornell.cs.apl.viaduct.Main'
}

task lexer {
    doLast {
        ant.taskdef(classname: 'jflex.anttask.JFlexTask', name: 'flex', classpath: configurations.jflex.asPath)
        ant.flex(file: 'src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/ImpLexer.flex')
    }
}

task parser {
    doLast {
        ant.taskdef(classname: "java_cup.anttask.CUPTask", name: "cup", classpath: configurations.cup.asPath)
        ant.cup(srcfile: 'src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/ImpParser.cup',
                destdir: 'src/main/java',
                parser: 'ImpParser')
    }
}

compileJava {
    dependsOn lexer
    dependsOn parser
}

jar {
    manifest {
        attributes 'Main-Class': 'edu.cornell.cs.apl.viaduct.Main'
    }
}

/*
shadowJar {
    baseName = 'viaduct'
}
*/

checkstyle {
    maxWarnings 0
    configProperties = [ "suppressionFile" : project(':').file('config/checkstyle/suppressions.xml')]
}

/*
spotless {
    java {
        googleJavaFormat()
        paddedCell()
    }
}
*/

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
    excludeFilter = file("$rootProject.projectDir/config/spotbugs/excludeFilter.xml")
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

editorconfig {
    excludes = [
        'gradlew.bat',
        'src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/*',
        'config/*',
        'bin/*'
    ]
}

dependencies {
    jflex group: 'de.jflex', name: 'jflex', version: '1.7.0'
    cup group: 'com.github.vbmacher', name: 'java-cup', version: '11b'
    cup group: 'com.github.vbmacher', name: 'java-cup-runtime', version: '11b'

    compileClasspath group: 'de.jflex', name: 'jflex', version: '1.7.0'
    compileClasspath group: 'com.github.vbmacher', name: 'java-cup', version: '11b'
    compileClasspath group: 'com.github.vbmacher', name: 'java-cup-runtime', version: '11b'
    implementation group: 'guru.nidi', name: 'graphviz-java', version: '0.8.3'
    implementation group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.8.1'
    implementation group: 'de.jflex', name: 'jflex', version: '1.7.0'
    implementation group: 'com.github.vbmacher', name: 'java-cup', version: '11b'
    implementation group: 'com.github.vbmacher', name: 'java-cup-runtime', version: '11b'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

check.dependsOn editorconfigCheck
