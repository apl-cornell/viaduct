plugins {
    id 'java'
    id 'application'
//    id 'com.github.johnrengelman.shadow' version '5.0.0'

    // Style checking
    id 'checkstyle'
    id "com.diffplug.gradle.spotless" version "3.18.0"
    id "org.ec4j.editorconfig" version "0.0.3"

    // Bug finding
    id 'com.github.spotbugs' version '1.6.9'
    // id 'pmd'
    id 'jacoco'

    // Lexing & Parsing
    id "org.xbib.gradle.plugin.jflex" version "1.2.0"
    id "cup.gradle.cup-gradle-plugin" version "1.2"
}

group 'edu.cornell.cs.apl'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

application {
    mainClassName = 'edu.cornell.cs.apl.viaduct.Main'
}

jar {
    manifest {
        attributes 'Main-Class': 'edu.cornell.cs.apl.viaduct.Main'
    }
}

//shadowJar {
//    baseName = 'viaduct'
//}


checkstyle {
    maxWarnings 0
    configProperties = ["suppressionFile": project.file("config/checkstyle/suppressions.xml")]
}

spotless {
    java {
        target project.fileTree(project.rootDir) {
            include '**/*.java'
            exclude "build"
        }
        googleJavaFormat()
        paddedCell()
    }
}


tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
    excludeFilter = project.file("config/spotbugs/excludeFilter.xml")
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

test {
    useJUnitPlatform()

    // Rerun tests when code examples change.
    inputs.files(project.fileTree(dir: "examples"))
}


editorconfig {
    excludes = [
        'gradlew.bat',
        'config/ * ',
    ]
}

cup {
    args = [
        "-package", "edu.cornell.cs.apl.viaduct.imp.parser",
        "-interface",
        "-parser", "ImpParser"
    ]
}

dependencies {
    // Functional data structures
    implementation group: 'io.vavr', name: 'vavr', version: '0.9.3'

    // DOT graph output
    implementation group: 'guru.nidi', name: 'graphviz-java', version: '0.8.3'

    // Command-line-argument parsing
    implementation group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.8.1'

    // Unit testing
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.4.2")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.4.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.4.2")

}

check {
    dependsOn editorconfigCheck
}
