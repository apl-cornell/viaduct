plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.0.0'

    id 'checkstyle'
    id "com.diffplug.gradle.spotless" version "3.18.0"
    id 'com.github.spotbugs' version '1.6.9'
    // id 'pmd'

    id 'jacoco'
    id "org.ec4j.editorconfig" version "0.0.3"
}

group 'edu.cornell.cs.apl'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

application {
    mainClassName = 'edu.cornell.cs.apl.viaduct.Main'
}

jar {
    manifest {
        attributes 'Main-Class': 'edu.cornell.cs.apl.viaduct.Main'
    }
}

shadowJar {
    baseName = 'viaduct'
}


task parser(type: Exec) {
    workingDir '.'
    inputs.file(file("src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/ImpLexer.flex"))
    inputs.file(file("src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/ImpParser.cup"))
    outputs.file(file("src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/ImpParser.java"))
    outputs.file(file("src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/sym.java"))
    commandLine('java', '-jar', 'lib/java_cup.jar', '-destdir',
        'src/main/java/edu/cornell/cs/apl/viaduct/imp/parser', '-package',
        'edu.cornell.cs.viaduct.imp.parser', '-interface', '-parser', 'ImpParser',
        'src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/ImpParser.cup'
    )
}

task lexer(type: Exec) {
    /* lexer uses symbols defined in parser! */
    dependsOn parser

    workingDir '.'
    inputs.file(file("src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/ImpLexer.flex"))
    outputs.file("src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/ImpLexer.java")
    commandLine('java', '-jar', 'lib/jflex-full-1.7.0.jar', '-d', 'src/main/java/edu/cornell/cs/apl/viaduct/imp/parser',
        'src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/ImpLexer.flex', '--nobak')
}

checkstyle {
    maxWarnings 0
    configProperties = ["suppressionFile": project(':').file('config/checkstyle/suppressions.xml')]
}

spotless {
    java {
        target project.fileTree(project.rootDir) {
            include '**/*.java'
            exclude 'src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/ImpParser.java',
                'src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/sym.java',
                'src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/ImpLexer.java'
        }
        googleJavaFormat()
        paddedCell()
    }
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
    excludeFilter = file("$rootProject.projectDir/config/spotbugs/excludeFilter.xml")
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

editorconfig {
    excludes = [
        'gradlew.bat',
        'config/*',
        'src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/ImpParser.java',
        'src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/sym.java',
        'src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/ImpLexer.java',
    ]
}

dependencies {
    implementation group: 'guru.nidi', name: 'graphviz-java', version: '0.8.3'
    implementation group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.8.1'
    implementation files('lib/java_cup.jar')

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

check {
    dependsOn editorconfigCheck
}

compileJava {
    dependsOn lexer
    dependsOn parser
}

clean {
    delete 'src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/ImpLexer.java'
    delete 'src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/ImpParser.java'
    delete 'src/main/java/edu/cornell/cs/apl/viaduct/imp/parser/sym.java'
}

