host alice: {A & B<-}
host bob:   {B & A<-}

/* two-round bidding for n items */
process main {
  val n: int{A ⊓ B} = 10; /* number of items to bid */
  val abids1 = Array[int]{A & B<-}(n);
  val abids2 = Array[int]{A & B<-}(n);
  val bbids1 = Array[int]{B & A<-}(n);
  val bbids2 = Array[int]{B & A<-}(n);

  /* round 1 */
  for (var i: int{A ⊓ B} = 0; i < n; i += 1) {
    abids1[i] = input int from alice;
    bbids1[i] = input int from bob;
  }

  /* reveal first-round winners */
  for (var i: int{A ⊓ B} = 0; i < n; i += 1) {
    val winner: bool = declassify abids1[i] < bbids1[i] to {A ⊓ B};
    output winner to alice;
    output winner to bob;
  } 

  /* round 2 */
  for (var i: int{A ⊓ B} = 0; i < n; i += 1) {
    abids2[i] = input int from alice;
    bbids2[i] = input int from bob;
  }

  /* reveal overall winners */
  for (var i: int{A ⊓ B} = 0; i < n; i += 1) {
    val abid: int{A & B<-} = (abids1[i] + abids2[i]) / 2;
    val bbid: int{B & A<-} = (bbids1[i] + bbids2[i]) / 2;
    val winner: bool{A ⊓ B} = declassify abid < bbid to {A ⊓ B};
    output winner to alice;
    output winner to bob;
  } 
}
