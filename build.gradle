import com.github.spotbugs.SpotBugsTask

plugins {
    id 'java'
    id 'application'
//    id 'com.github.johnrengelman.shadow' version '5.0.0'

    // Style checking
    id 'checkstyle'
    id "com.diffplug.gradle.spotless" version "3.18.0"
    id "org.ec4j.editorconfig" version "0.0.3"

    // Bug finding
    id 'com.github.spotbugs' version '1.6.9'
//    id 'pmd'
    id 'jacoco'

    // Lexing & Parsing
    id "org.xbib.gradle.plugin.jflex" version "1.2.0"
    id "cup.gradle.cup-gradle-plugin" version "1.2"
}

group 'edu.cornell.cs.apl'
version '0.1'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

application {
    mainClassName = "${project.group}.${project.name}.Main"
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClassName
    }
}

checkstyle {
    maxWarnings 0
    configDir project.file("config/checkstyle")
}

spotless {
    java {
        // TODO: spotlessCheck doesn't check anything! spotlessApply will reformat though. Ugh.
        googleJavaFormat()
        paddedCell()
        target 'src/**/*.java'
    }
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
    excludeFilter = project.file("config/spotbugs/excludeFilter.xml")
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

test {
    useJUnitPlatform()

    // Rerun tests when code examples change.
    inputs.files(project.fileTree(dir: "examples"))
}


editorconfig {
    excludes = [
        'gradlew.bat',
        'out',
    ]
}

cup {
    args = [
        "-package", "edu.cornell.cs.apl.viaduct.imp.parser",
        "-interface",
        "-parser", "ImpParser"
    ]
}

dependencies {
    // Functional data structures
    implementation group: 'io.vavr', name: 'vavr', version: '0.9.3'

    // Graphs
    implementation group: 'org.jgrapht', name: 'jgrapht-core', version: '1.3.1'

    // Command-line-argument parsing
    implementation group: 'com.github.rvesse', name: 'airline', version: '2.7.0'

    // DOT graph output
    implementation group: 'guru.nidi', name: 'graphviz-java', version: '0.8.3'

    // Logging (disabled for now using the NOP engine)
    implementation group: 'org.slf4j', name: 'slf4j-nop', version: '1.8.0-beta4'

    // Google's AutoValue
    compile group: 'com.google.auto.value', name: 'auto-value-annotations', version: '1.6.2'
    annotationProcessor group: 'com.google.auto.value', name: 'auto-value', version: '1.6.2'

    // Unit testing
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.4.2")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.4.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.4.2")
}

check {
    dependsOn editorconfigCheck
}
