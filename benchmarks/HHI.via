/*
 * Calculates the Herfindahl-Hirschman index for market competitiveness.
 * In this scenario, we're assessing the competitiveness of the widget market
 * amongst two widget makers by calculating the revenue of each company
 * (by summing up sales in n stores) and then calculating each company's
 * market share and applying the HHI formula.
 */
process main {
    val storeCount: int = 100;

    val a = Array[int]{A & B<-}(storeCount);
    for (var i : int = 0; i < storeCount; i += 1) {
        a[i] = input int from companyA;
    }

    val b = Array[int]{B & A<-}(storeCount);
    for (var i : int = 0; i < storeCount; i += 1) {
        b[i] = input int from companyB;
    }

    var a_rev : int{A & B<-} = 0;
    for (var i : int = 0; i < storeCount; i += 1) {
        a_rev += a[i];
    }

    var b_rev : int{B & A<-} = 0;
    for (var i : int = 0; i < storeCount; i += 1) {
        b_rev += b[i];
    }

    val total_market : int = a_rev + b_rev;

    val a_share : int = (100 * a_rev) / total_market;
    val b_share : int = (100 * b_rev) / total_market;

    val hhi : int = (a_share * a_share) + (b_share * b_share);
    val hhi_public : int = declassify hhi to {A âŠ“ B};

    output hhi_public to companyA;
    output hhi_public to companyB;
}

host companyA  : {A & B<-}
host companyB  : {B & A<-}
