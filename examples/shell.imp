process main {
  cin : int{C};
  ain : int{A};
  shell : int{C & A<-};
  guess : int{A & C<-};
  win : int{(A | C)-> & (A & C)<-};

  cin <- recv chuck_main;
  ain <- recv alice_main;

  /* Chuck and Alice must endorse their inputs to `shell` and `guess`, respectively. */
  shell = endorse(cin, {C & A<-});
  guess = endorse(ain, {A & C<-});

  /* Chuck must prove that `shell` is valid without revealing its value. */
  win_secret : int{A & C};
  if (1 <= shell && shell <= 3) {
    if (shell == guess) {
      win_secret = 1;

    } else {
      win_secret = 0;
    }

  } else {
      win_secret = -1;
  }

  win = declassify(win_secret, {(A | C)-> & (A & C)<-});
}

process chuck_main {
  send 1 to main;
}

process alice_main {
  send 2 to main;
}

/* Declare the actual machines (i.e. hosts) we have. */
host alice : {A};
host chuck : {C};
