host alice
host bob


assume for <- alice trusts bob
assume for <- bob trusts alice

/* two-round bidding for n items */
fun main() {
  val n: int = 10; /* number of items to bid */
  val abids1 = Array[int](n);
  val abids2 = Array[int](n);
  val bbids1 = Array[int](n);
  val bbids2 = Array[int](n);

  /* round 1 */
  for (var i: int = 0; i < n; i += 1) {
    abids1[i] = input int from alice;
    bbids1[i] = input int from bob;
  }

  /* reveal first-round winners */
  for (var i: int = 0; i < n; i += 1) {
    val winner: bool = declassify abids1[i] < bbids1[i] to {alice ⊓ bob};
    output winner to alice;
    output winner to bob;
  }

  /* round 2 */
  for (var i: int = 0; i < n; i += 1) {
    abids2[i] = input int from alice;
    bbids2[i] = input int from bob;
  }

  /* reveal overall winners */
  for (var i: int = 0; i < n; i += 1) {
    val abid: int = (abids1[i] + abids2[i]) / 2;
    val bbid: int = (bbids1[i] + bbids2[i]) / 2;
    val winner: bool = declassify abid < bbid to {alice ⊓ bob};
    output winner to alice;
    output winner to bob;
  }
}
