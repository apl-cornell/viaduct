process main {
    /*
     * two millionaires, Alice and Bob, vie to buy a painting and
     * the seller, Chuck, will only sell to the richer person.
     * however, Chuck personally doesn't like Alice and will charge
     * her more if she wins. however, Alice and Chuck privately have
     * maximum spending limits and will refuse to buy the painting if
     * it is above that limit. */
    int{A} a[5] <- recv @alice;
    int{A} a_limit <- recv @alice;

    int{B} b[5] <- recv @bob;
    int{B} b_limit <- recv @bob;

    int{C} total_price <- recv @chuck;

    int a_sum = 0;
    for (int ai = 0; ai < 5; ai++) {
        a_sum += a[ai];
    }

    int b_sum = 0;
    for (int bi = 0; bi < 5; bi++) {
        b_sum += b[bi];
    }

    bool a_richer = declassify(endorse(a_sum, {A & B<- & C<-}) > endorse(b_sum, {B & A<- & C<-}), {A ⊓ B ⊓ C});
    bool{C} charge_more = a_richer;

    /* Chuck doesn't like Alice, so he charges her more */
    if (charge_more) {
        total_price += 25000;
    }

    int{A & B & C} payment_due = endorse(total_price, {C & A<- & B<-});

    /* possible values
     * result = 0   : no one buys the painting
     * result = -1  : Alice buys the painting
     * result = 1   : Bob buys the painting
     */
    int{A ⊓ B ⊓ C} result = 0;
    int a_limit_trusted = endorse(a_limit, {A & B<- & C<-});
    int b_limit_trusted = endorse(b_limit, {B & A<- & C<-});

    if (a_richer) {
        if (declassify(payment_due <= a_limit_trusted, {A ⊓ B ⊓ C})) {
            result = -1;
        }

    } else {
        if (declassify(payment_due <= b_limit_trusted, {A ⊓ B ⊓ C})) {
            result = 1;
        }
    }

    send result to @alice;
    send result to @bob;
    send result to @chuck;
}

host alice : {A};
host bob   : {B};
host chuck : {C};
