import edu.cornell.cs.apl.viaduct.errors.NoMainError;
import edu.cornell.cs.apl.viaduct.errors.ParsingError;
import edu.cornell.cs.apl.viaduct.protocols.*;
import edu.cornell.cs.apl.viaduct.security.*;
import edu.cornell.cs.apl.viaduct.syntax.*;
import edu.cornell.cs.apl.viaduct.syntax.datatypes.*;
import edu.cornell.cs.apl.viaduct.syntax.surface.*;
import edu.cornell.cs.apl.viaduct.syntax.operators.*;
import edu.cornell.cs.apl.viaduct.syntax.types.*;
import edu.cornell.cs.apl.viaduct.syntax.values.*;

import java.util.List;
import java.util.LinkedList;
import java.util.Map;
import kotlin.collections.CollectionsKt;
import kotlin.Pair;

import java_cup.runtime.ComplexSymbolFactory;
import java_cup.runtime.ComplexSymbolFactory.Location;
import java_cup.runtime.Symbol;


action code {:
    /** Constructs a binary expression node. Source location is inferred from the arguments. */
    private OperatorApplicationNode binaryExpression(BinaryOperator op, ExpressionNode lhs, ExpressionNode rhs) {
        Arguments arguments = Arguments.from(lhs, rhs);
        SourceRange sourceLocation = lhs.getSourceLocation().merge(rhs.getSourceLocation());
        return new OperatorApplicationNode(op, arguments, sourceLocation);
    }

    /** Constructs a label argument list from a single optional label. */
    private Arguments<Located<LabelExpression>> optionalLabelArgument(Located<LabelExpression> label) {
        if (label == null) {
            return null;
        } else {
            return Arguments.from(label);
        }
    }
:}

parser code {:
    Map<ProtocolName, ProtocolParser<Protocol>> protocolParsers;

    /** Generates source location information for a node given left and right character offsets. */
    private SourceRange location(int left, int right) {
        return ((Lexer) getScanner()).location(left, right);
    }

    @Override
    public void report_error(String message, Object info) {
        report_fatal_error(message, info);
    }

    @Override
    public void report_fatal_error(String message, Object info) {
        done_parsing();

        final Symbol infoSymbol = (Symbol) info;
        final SourceRange errorLocation = location(infoSymbol.left, infoSymbol.right);
        final String actualToken = symbl_name_from_id(infoSymbol.sym);
        final List<String> expectedTokens = new LinkedList<>();
        for (Integer id : expected_token_ids()) {
            expectedTokens.add(symbl_name_from_id(id));
        }
        throw new ParsingError(errorLocation, actualToken, expectedTokens);
    }


    // Used to support multiple start symbols.
    private Symbol current_symbol;

    private void set_current_symbol(int symbol) {
        final Location location = new Location(0, 0, 0);
        current_symbol =
            ((ComplexSymbolFactory) getSymbolFactory())
                .newSymbol(symbl_name_from_id(symbol), symbol, location, location);
    }

    /** Parses the input as a program. */
    public ProgramNode parseProgram() throws Exception {
        set_current_symbol(sym.PARSE_PROGRAM);
        return (ProgramNode) parse().value;
    }

    /** Parses the input as a security label. */
    public LabelExpression parseLabel() throws Exception {
        set_current_symbol(sym.PARSE_LABEL);
        return (LabelExpression) parse().value;
    }
:}

scan with {:
    if (current_symbol != null) {
        final Symbol result = current_symbol;
        current_symbol = null;
        return result;
    }
    return getScanner().next_token();
:};

nonterminal ProgramNode program;
nonterminal List<TopLevelDeclarationNode> declaration_list;
nonterminal TopLevelDeclarationNode declaration;

nonterminal SimpleStatementNode simple_stmt;
nonterminal ExpressionNode optional_initial_value;
nonterminal BinaryOperator compound_assignment;
nonterminal StatementNode stmt;
nonterminal BlockNode block;
nonterminal List<StatementNode> stmt_list;

nonterminal ExpressionNode expr;
nonterminal List<ExpressionNode> expr_list, nonempty_expr_list;
nonterminal Located<ArgumentLabel> argument_label;
nonterminal IndexingNode indexing;

nonterminal Value value;
nonterminal Located<ValueType> value_type;
nonterminal Located<LabelExpression> label, optional_label, optional_from_label, optional_to_label;
nonterminal Located<LabelExpression> label_signature, optional_label_signature;
nonterminal LabelExpression label_expr, label_expr_noparam, label_expr_signature;

nonterminal Located<ObjectVariable> variable;
nonterminal Located<Host> host;
// nonterminal List<Located<Host>> host_list, nonempty_host_list;
// TODO: the above type causes a parse error in the IntelliJ plugin
nonterminal List host_list, nonempty_host_list;
nonterminal HostSetValue host_set;

nonterminal List parameter_list, nonempty_parameter_list;
nonterminal ParameterNode parameter;
nonterminal ParameterDirection parameter_direction;

nonterminal List argument_list, nonempty_argument_list;
nonterminal FunctionArgumentNode argument;

nonterminal Located<Protocol> protocol;
nonterminal Located<Protocol> protocol_annot;
nonterminal Located<ProtocolName> protocol_name;
// TODO: more accurate types cause parse errors in IntelliJ
//nonterminal List<Pair<Located<ArgumentLabel>, Located<Value>>> protocol_argument_list, nonempty_protocol_argument_list;
//nonterminal Pair<Located<ArgumentLabel>, Located<Value>> protocol_labelled_argument;
nonterminal List protocol_argument_list, nonempty_protocol_argument_list;
nonterminal Pair<Located, Located> protocol_labelled_argument;
nonterminal Value protocol_argument;

terminal String IDENT, CAP_IDENT;
terminal String TMP_IDENT;
terminal int INT_LIT;
terminal TRUE, FALSE;

terminal HOST, PROCESS;
terminal CAP_HOST;
terminal FUNCTION;
terminal OUT;
terminal COLON;

terminal EQ;
terminal PLUSEQ, MINUSEQ, TIMESEQ, DIVIDEEQ;
terminal LET, VAL, VAR;
terminal IF, ELSE, WHILE, FOR, LOOP, BREAK;
terminal INPUT, RECEIVE, FROM, OUTPUT, SEND, TO;
terminal ASSERT;

terminal NOT, ANDAND, OROR;
terminal EQEQ, NEQ, LT, LEQ, GT, GEQ;
terminal PLUS, MINUS, TIMES, DIVIDE;
terminal MIN, MAX, MUX;

terminal DECLASSIFY, ENDORSE, DOWNGRADE;

terminal INT, BOOL, UNIT, ARRAY;
terminal TOP, BOTTOM, OR, AND, JOIN, MEET, RARROW, LARROW;
terminal AT;

terminal OPEN_BRACE, CLOSE_BRACE, SEMICOLON;
terminal OPEN_SQBRACE, CLOSE_SQBRACE;
terminal OPEN_PAREN, CLOSE_PAREN, COMMA;

// Used to support multiple start symbols
nonterminal entry_point;
terminal Symbol PARSE_PROGRAM, PARSE_LABEL;


precedence left ANDAND, OROR, AND, OR, JOIN, MEET;
precedence nonassoc EQEQ, NEQ, LT, LEQ, GT, GEQ;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence right NOT, MIN;
precedence left LARROW, RARROW;
precedence nonassoc OPEN_PAREN, CLOSE_PAREN;

start with entry_point;

entry_point ::=
     PARSE_PROGRAM program:s    {: RESULT = s; :}
  |  PARSE_LABEL   label_expr:s {: RESULT = s; :}
  ;

program ::=
    declaration_list:declarations {:
      if (declarations.isEmpty()) {
        /* Empty program */
        throw new NoMainError(((Lexer) getScanner()).sourceFile.getPath());
      }


      RESULT = new ProgramNode(declarations, location(declarationsleft, declarationsright));
    :}
  ;

declaration_list ::=
    declaration_list:declarations declaration:declaration {:
      declarations.add(declaration);
      RESULT = declarations;
    :}

  | /* empty */ {:
      RESULT = CollectionsKt.mutableListOf();
    :}
  ;

declaration ::=
    HOST:begin host:host COLON label:authority {:
      RESULT = new HostDeclarationNode(host, authority, location(beginleft, authorityright));
    :}

  | PROCESS:begin protocol:protocol block:body {:
      RESULT = new ProcessDeclarationNode(protocol, body, location(beginleft, bodyright));
    :}

  | FUNCTION:begin IDENT:funcname optional_label_signature:label
    OPEN_PAREN:paramsbegin parameter_list:params CLOSE_PAREN:paramsclose block:body {:
      RESULT = new FunctionDeclarationNode(
        new Located(new FunctionName(funcname), location(funcnameleft, funcnameright)),
        label,
        new Arguments(params, location(paramsbeginleft, paramsbeginright)),
        body,
        location(beginleft, bodyright)
      );
  :}
  ;

parameter_list ::=
    nonempty_parameter_list:params {:
      RESULT = params;
    :}

  | /* empty */ {:
      RESULT = CollectionsKt.mutableListOf();
    :}
  ;

nonempty_parameter_list ::=
    nonempty_parameter_list:params COMMA parameter:param {:
      params.add(param);
      RESULT = params;
    :}

  | parameter:param {:
      RESULT = CollectionsKt.mutableListOf(param);
    :}
  ;

parameter_direction ::=
    OUT         {: RESULT = ParameterDirection.PARAM_OUT; :}
  | /* empty */ {: RESULT = ParameterDirection.PARAM_IN; :}
  ;

parameter ::=
    variable:name COLON value_type:type optional_label_signature:label protocol_annot:protocol {:
      RESULT = new ParameterNode(
        name,
        ParameterDirection.PARAM_IN,
        new Located(PrimitivesKt.getImmutableCell(), name.getSourceLocation()),
        Arguments.from(type),
        optionalLabelArgument(label),
        protocol,
        location(nameleft, labelright)
      );
    :}

  | variable:name COLON OUT value_type:type label_signature:label protocol_annot:protocol {:
      RESULT = new ParameterNode(
        name,
        ParameterDirection.PARAM_OUT,
        new Located(PrimitivesKt.getImmutableCell(), name.getSourceLocation()),
        Arguments.from(type),
        optionalLabelArgument(label),
        protocol,
        location(nameleft, labelright)
      );
    :}

  | variable:name COLON ARRAY OPEN_SQBRACE value_type:elementType CLOSE_SQBRACE
    optional_label_signature:label protocol_annot:protocol {:
      RESULT = new ParameterNode(
        name,
        ParameterDirection.PARAM_IN,
        new Located(PrimitivesKt.getVector(), name.getSourceLocation()),
        Arguments.from(elementType),
        optionalLabelArgument(label),
        protocol,
        location(nameleft, labelright)
      );
    :}

  | variable:name COLON OUT ARRAY OPEN_SQBRACE value_type:elementType CLOSE_SQBRACE
    label_signature:label protocol_annot:protocol {:
      RESULT = new ParameterNode(
        name,
        ParameterDirection.PARAM_OUT,
        new Located(PrimitivesKt.getVector(), name.getSourceLocation()),
        Arguments.from(elementType),
        optionalLabelArgument(label),
        protocol,
        location(nameleft, labelright)
      );
    :}
  ;

simple_stmt ::=
    /* Immutable variable declaration. */
    VAL:begin variable:var COLON value_type:type optional_label:label protocol_annot:protocol EQ expr:value {:
      RESULT = new DeclarationNode(
        var,
        new ConstructorCallNode(
            new Located(PrimitivesKt.getImmutableCell(), var.getSourceLocation()),
            Arguments.from(type),
            optionalLabelArgument(label),
            protocol,
            Arguments.from(value),
            location(typeleft, valueright)
        ),
        location(beginleft, valueright));
    :}

    /* Mutable variable declaration. */
  | VAR:begin variable:var COLON value_type:type optional_label:label protocol_annot:protocol optional_initial_value:initial {:
      final LiteralNode defaultValue = new LiteralNode(
        type.getValue().getDefaultValue(),
        type.getSourceLocation());

      RESULT = new DeclarationNode(
        var,
        new ConstructorCallNode(
            new Located(PrimitivesKt.getMutableCell(), var.getSourceLocation()),
            Arguments.from(type),
            optionalLabelArgument(label),
            protocol,
            Arguments.from((initial == null) ? defaultValue : initial),
            location(typeleft, initialright)
        ),
        location(beginleft, initialright));
    :}

    /* Generic object declaration. */
  | VAL:begin variable:var EQ expr:e {:
      RESULT = new DeclarationNode(
        var,
        e,
        location(beginleft, eright));
    :}

  | indexing:indexing EQ:eq expr:e {:
      RESULT = indexing.set(e, location(eqleft, eqright));
    :}

  | indexing:indexing compound_assignment:op expr:e {:
      RESULT = indexing.modify(op, e, location(opleft, opright));
        :}

  /* out parameter declaration */
  | OUT:begin IDENT:id EQ expr:e {:
      RESULT = new OutParameterInitializationNode(
        new Located(new ObjectVariable(id), location(idleft, idright)),
        e,
        location(beginleft, eright));
    :}

  /* function call */
  | IDENT:funcname OPEN_PAREN:paramsopen argument_list:args CLOSE_PAREN:end {:
      RESULT = new FunctionCallNode(
        new Located(new FunctionName(funcname), location(funcnameleft, funcnameright)),
        new Arguments(args, location(paramsopenleft, endright)),
        location(funcnameleft, endright)
      );
    :}

  /* let binding */
  | LET:begin TMP_IDENT:id protocol_annot:protocol EQ expr:e {:
    RESULT = new LetNode(
      new Located(new Temporary(id), location(idleft, idright)),
      e,
      protocol,
      location(beginleft, eright));
    :}

  | OUTPUT:begin expr:message TO host:recipient {:
      RESULT = new OutputNode(message, recipient, location(beginleft, recipientright));
    :}

  | SEND:begin expr:message TO protocol:recipient {:
      RESULT = new SendNode(message, recipient, location(beginleft, recipientright));
    :}
  ;

optional_initial_value ::=
    EQ expr:e    {: RESULT = e; :}
  | /* empty */  {: RESULT = null; :}
  ;

compound_assignment ::=
    PLUSEQ   {: RESULT = Addition.INSTANCE; :}
  | MINUSEQ  {: RESULT = Subtraction.INSTANCE; :}
  | TIMESEQ  {: RESULT = Multiplication.INSTANCE; :}
  | DIVIDEEQ {: RESULT = Division.INSTANCE; :}
  ;

argument ::=
    expr:expr {:
      RESULT = new ExpressionArgumentNode(expr, expr.getSourceLocation());
    :}

  | AND:begin variable:var {:
      RESULT = new ObjectReferenceArgumentNode(var, location(beginleft, varright));
    :}

  | VAL:begin variable:var {:
      RESULT = new ObjectDeclarationArgumentNode(var, location(beginleft, varright));
    :}

  | OUT:begin variable:var {:
      RESULT = new OutParameterArgumentNode(var, location(beginleft, varright));
    :}
  ;

nonempty_argument_list ::=
    nonempty_argument_list:args COMMA argument:arg {:
      args.add(arg);
      RESULT = args;
    :}

  | argument:arg {:
      RESULT = CollectionsKt.mutableListOf(arg);
    :}
  ;

argument_list ::=
    nonempty_argument_list:args {:
      RESULT = args;
    :}

  | /* empty */ {:
      RESULT = CollectionsKt.mutableListOf();
    :}
  ;

stmt ::=
    simple_stmt:stmt SEMICOLON {:
      RESULT = stmt;
    :}

  | IF:begin OPEN_PAREN expr:guard CLOSE_PAREN block:thenCase ELSE block:elseCase {:
      RESULT = new IfNode(guard, thenCase, elseCase, location(beginleft, elseCaseright));
    :}

  | IF:begin OPEN_PAREN expr:guard CLOSE_PAREN block:thenCase {:
      SourceRange sourceLocation = location(beginleft, thenCaseright);
      RESULT = new IfNode(
        guard,
        thenCase,
        new BlockNode(CollectionsKt.listOf(), sourceLocation),
        sourceLocation);
    :}

  | WHILE:begin OPEN_PAREN expr:guard CLOSE_PAREN block:body {:
      RESULT = new WhileLoopNode(guard, body, null, location(beginleft, bodyright));
    :}

  | FOR:begin OPEN_PAREN simple_stmt:initialize SEMICOLON expr:guard SEMICOLON simple_stmt:update CLOSE_PAREN block:body {:
      RESULT = new ForLoopNode(initialize, guard, update, body, null, location(beginleft, bodyright));
    :}

  | LOOP:begin block:body {:
      RESULT = new InfiniteLoopNode(body, null, location(beginleft, bodyright));
    :}

  | BREAK:brk SEMICOLON {:
      RESULT = new BreakNode(null, location(brkleft, brkright));
    :}

  | ASSERT:begin expr:e SEMICOLON {:
      RESULT = new AssertionNode(e, location(beginleft, eright));
    :}

  | block:block {:
      RESULT = block;
    :}
  ;

block ::=
    OPEN_BRACE:begin stmt_list:statements CLOSE_BRACE:end {:
      RESULT = new BlockNode(statements, location(beginleft, endright));
    :}
  ;

stmt_list ::=
    stmt_list:slist stmt:s {:
      slist.add(s);
      RESULT = slist;
    :}

  | /* empty */ {:
      RESULT = CollectionsKt.mutableListOf();
    :}
  ;


expr ::=
    value:value {:
      RESULT = new LiteralNode(value, location(valueleft, valueright));
    :}

  | indexing:indexing {:
       RESULT = indexing.get();
    :}

  | TMP_IDENT:id {:
      RESULT = new ReadNode(new Located(new Temporary(id), location(idleft, idright)));
    :}

  | NOT:begin expr:e {:
      RESULT = new OperatorApplicationNode(
        Not.INSTANCE,
        Arguments.from(e),
        location(beginleft, eright));
    :}

  | MINUS:begin expr:e {:
      RESULT = new OperatorApplicationNode(
        Negation.INSTANCE,
        Arguments.from(e),
        location(beginleft, eright));
    :}

  | expr:e1 ANDAND expr:e2 {:
      RESULT = binaryExpression(And.INSTANCE, e1, e2);
    :}

  | expr:e1 OROR expr:e2 {:
      RESULT = binaryExpression(Or.INSTANCE, e1, e2);
    :}

  | expr:e1 AND expr:e2 {:
      RESULT = binaryExpression(ExclusiveOr.INSTANCE, e1, e2);
    :}

  | expr:e1 EQEQ expr:e2 {:
      RESULT = binaryExpression(EqualTo.INSTANCE, e1, e2);
    :}

  | expr:e1 NEQ expr:e2 {:
      RESULT = binaryExpression(ExclusiveOr.INSTANCE, e1, e2);
    :}

  | expr:e1 LT expr:e2 {:
      RESULT = binaryExpression(LessThan.INSTANCE, e1, e2);
    :}

  | expr:e1 LEQ expr:e2 {:
      RESULT = binaryExpression(LessThanOrEqualTo.INSTANCE, e1, e2);
    :}

  | expr:e1 GT expr:e2 {:
      RESULT = binaryExpression(GreaterThan.INSTANCE, e1, e2);
    :}

  | expr:e1 GEQ expr:e2 {:
      RESULT = binaryExpression(GreaterThanOrEqualTo.INSTANCE, e1, e2);
    :}

  | expr:e1 PLUS expr:e2 {:
      RESULT = binaryExpression(Addition.INSTANCE, e1, e2);
    :}

  | expr:e1 MINUS expr:e2 {:
      RESULT = binaryExpression(Subtraction.INSTANCE, e1, e2);
    :}

  | expr:e1 TIMES expr:e2 {:
      RESULT = binaryExpression(Multiplication.INSTANCE, e1, e2);
    :}

  | expr:e1 DIVIDE expr:e2 {:
      RESULT = binaryExpression(Division.INSTANCE, e1, e2);
    :}

  | MIN:begin OPEN_PAREN expr:e1 COMMA expr:e2 CLOSE_PAREN:end {:
      RESULT = binaryExpression(Minimum.INSTANCE, e1, e2);
    :}

  | MAX:begin OPEN_PAREN expr:e1 COMMA expr:e2 CLOSE_PAREN:end {:
      RESULT = binaryExpression(Maximum.INSTANCE, e1, e2);
    :}

  | MUX:begin OPEN_PAREN expr:e1 COMMA expr:e2 COMMA expr:e3 CLOSE_PAREN:end {:
      Arguments arguments = Arguments.from(e1, e2, e3);
      SourceRange sourceLocation = e1.getSourceLocation().merge(e2.getSourceLocation()).merge(e3.getSourceLocation());
      RESULT = new OperatorApplicationNode(Mux.INSTANCE, arguments, location(beginleft, endright));
    :}

  | DECLASSIFY:begin expr:e optional_from_label:from TO label:to {:
      RESULT = new DeclassificationNode(e, from, to, location(beginleft, toright));
    :}

    // invert FROM and TO labels to prevent shift-reduce conflict
  | ENDORSE:begin expr:e optional_to_label:to FROM label:from {:
      RESULT = new EndorsementNode(e, from, to, location(beginleft, toright));
    :}

  | INPUT:begin value_type:type FROM host:sender {:
      RESULT = new InputNode(type, sender, location(beginleft, senderright));
    :}

  | RECEIVE:begin value_type:type FROM protocol:sender {:
      RESULT = new ReceiveNode(type, sender, location(beginleft, senderright));
    :}

  | OPEN_PAREN expr:e CLOSE_PAREN {:
      RESULT = e;
    :}

  // constructor call, make this general later
  | ARRAY:begin OPEN_SQBRACE value_type:elementType CLOSE_SQBRACE
    optional_label:label protocol_annot:protocol OPEN_PAREN expr:size CLOSE_PAREN:end
    {:
      RESULT = new ConstructorCallNode(
        new Located(PrimitivesKt.getVector(), location(beginleft, beginright)),
        Arguments.from(elementType),
        optionalLabelArgument(label),
        protocol,
        Arguments.from(size),
        location(beginleft, endright)
      );
    :}
  ;

expr_list ::=
    nonempty_expr_list:elist {:
      RESULT = elist;
    :}

  | /* empty */ {:
      RESULT = CollectionsKt.listOf();
    :}
  ;

nonempty_expr_list ::=
    nonempty_expr_list:elist COMMA expr:e {:
      elist.add(e);
      RESULT = elist;
    :}

  | expr:e {:
      RESULT = CollectionsKt.mutableListOf(e);
    :}
  ;

argument_label ::=
    IDENT:label {:
      RESULT = new Located(new ArgumentLabel(label), location(labelleft, labelright));
    :}
  /* Parse the "host" keyword as a label also. */
  // TODO: find a more generic solution (lexer states?)
  | HOST:label {:
      RESULT = new Located(new ArgumentLabel("host"), location(labelleft, labelright));
    :}
  ;

indexing ::=
    variable:var {:
      RESULT = new IndexingNode(var, new Arguments(location(varleft, varright)));
    :}

  | variable:var OPEN_SQBRACE:open nonempty_expr_list:indices CLOSE_SQBRACE:close {:
      RESULT = new IndexingNode(var, new Arguments(indices, location(openleft, closeright)));
    :}
  ;


value ::=
    TRUE {:
      RESULT = new BooleanValue(true);
    :}

  | FALSE {:
      RESULT = new BooleanValue(false);
    :}

  | INT_LIT:literal {:
      RESULT = new IntegerValue(literal);
    :}

  | UNIT {:
      RESULT = UnitValue.INSTANCE;
    :}
  ;


value_type ::=
    INT:type {:
      RESULT = new Located(IntegerType.INSTANCE, location(typeleft, typeright));
    :}

  | BOOL:type {:
      RESULT = new Located(BooleanType.INSTANCE, location(typeleft, typeright));
    :}

  | UNIT:type {:
      RESULT = new Located(UnitType.INSTANCE, location(typeleft, typeright));
    :}
  ;


label ::=
    OPEN_BRACE:begin label_expr:l CLOSE_BRACE:end {:
      RESULT = new Located(l, location(beginleft, endright));
    :}
  ;

label_expr ::=
    CAP_IDENT:id {: RESULT = new LabelLiteral(id); :}

  | IDENT:id {: RESULT = new LabelParameter(id); :}

  | TOP {: RESULT = LabelTop.INSTANCE; :}

  | BOTTOM {: RESULT = LabelBottom.INSTANCE; :}

  | label_expr:l1 OR label_expr:l2 {:
      RESULT = new LabelOr(l1, l2);
    :}

  | label_expr:l1 AND label_expr:l2 {:
      RESULT = new LabelAnd(l1, l2);
    :}

  | label_expr:l1 JOIN label_expr:l2 {:
      RESULT = new LabelJoin(l1, l2);
    :}

  | label_expr:l1 MEET label_expr:l2 {:
      RESULT = new LabelMeet(l1, l2);
    :}

  | label_expr:l RARROW {:
      RESULT = new LabelConfidentiality(l);
    :}

  | label_expr:l LARROW {:
      RESULT = new LabelIntegrity(l);
    :}

  | OPEN_PAREN label_expr:l CLOSE_PAREN {:
      RESULT = l;
    :}
  ;

label_signature ::=
    OPEN_BRACE:begin label_expr_signature:l CLOSE_BRACE:end {:
      RESULT = new Located(l, location(beginleft, endright));
    :}
  ;

label_expr_signature ::=
    label_expr_noparam:l {: RESULT = l; :}

  | IDENT:id             {: RESULT = new LabelParameter(id); :}
  ;

label_expr_noparam ::=
    CAP_IDENT:id {: RESULT = new LabelLiteral(id); :}

  | TOP {: RESULT = LabelTop.INSTANCE; :}

  | BOTTOM {: RESULT = LabelBottom.INSTANCE; :}

  | label_expr:l1 OR label_expr:l2 {:
      RESULT = new LabelOr(l1, l2);
    :}

  | label_expr:l1 AND label_expr:l2 {:
      RESULT = new LabelAnd(l1, l2);
    :}

  | label_expr:l1 JOIN label_expr:l2 {:
      RESULT = new LabelJoin(l1, l2);
    :}

  | label_expr:l1 MEET label_expr:l2 {:
      RESULT = new LabelMeet(l1, l2);
    :}

  | label_expr:l RARROW {:
      RESULT = new LabelConfidentiality(l);
    :}

  | label_expr:l LARROW {:
      RESULT = new LabelIntegrity(l);
    :}

  | OPEN_PAREN label_expr:l CLOSE_PAREN {:
      RESULT = l;
    :}
  ;

optional_label ::=
    label:l     {: RESULT = l; :}
  | /* empty */ {: RESULT = null; :}
  ;

optional_label_signature ::=
    label_signature:l     {: RESULT = l; :}
  | /* empty */         {: RESULT = null; :}
  ;

optional_from_label ::=
    FROM label:l {: RESULT = l; :}
  | /* empty */  {: RESULT = null; :}
  ;

optional_to_label ::=
    TO label:l {: RESULT = l; :}
  | /* empty */  {: RESULT = null; :}
  ;


variable ::=
    IDENT:id  {:
      RESULT = new Located(new ObjectVariable(id), location(idleft, idright));
    :}
  ;

host ::=
    IDENT:id {:
      RESULT = new Located(new Host(id), location(idleft, idright));
    :}
  ;

host_list ::=
    nonempty_host_list:hosts {:
      RESULT = hosts;
    :}

  | /* empty */ {:
      RESULT = CollectionsKt.mutableListOf();
    :}
  ;

nonempty_host_list ::=
    nonempty_host_list:hosts COMMA host:host {:
      hosts.add(host);
      RESULT = hosts;
    :}

  | host:host {:
      RESULT = CollectionsKt.mutableListOf(host);
    :}
  ;

host_set ::=
    OPEN_BRACE host_list:hosts CLOSE_BRACE {:
      RESULT = HostSetValue.invoke(hosts);
    :}
  ;

protocol_annot ::=
    AT protocol:p     {: RESULT = p; :}
  | /* empty */       {: RESULT = null; :}
  ;

protocol ::=
    IDENT:id {:
      RESULT = new Located(new Ideal(id), location(idleft, idright));
    :}

  | CAP_HOST:begin OPEN_PAREN host:host CLOSE_PAREN:end {:
      RESULT = new Located(
        new HostInterface(host.getValue()),
        location(beginleft, endright));
    :}

  | protocol_name:protocolName OPEN_PAREN:open protocol_argument_list:arguments CLOSE_PAREN:close {:
      RESULT = new Located(
        ProtocolParserKt.parseProtocol(
          protocolParsers,
          protocolName,
          NamedArguments.invoke(arguments, location(openleft, closeright))),
        location(protocolNameleft, closeright));
    :}
  ;

protocol_name ::=
    CAP_IDENT:protocolName {:
      RESULT = new Located(new ProtocolName(protocolName), location(protocolNameleft, protocolNameright));
    :}
  ;

protocol_argument_list ::=
    nonempty_protocol_argument_list:arguments {:
      RESULT = arguments;
    :}

  | /* empty */ {:
      RESULT = CollectionsKt.listOf();
    :}
  ;

nonempty_protocol_argument_list ::=
    nonempty_protocol_argument_list:arguments COMMA protocol_labelled_argument:argument {:
      arguments.add(argument);
      RESULT = arguments;
    :}

  | protocol_labelled_argument:argument {:
      RESULT = CollectionsKt.mutableListOf(argument);
    :}
  ;

protocol_labelled_argument ::=
    argument_label:label EQ protocol_argument:value {:
      RESULT = new Pair(
        label,
        new Located(value, location(valueleft, valueright))
      );
    :}
  ;

protocol_argument ::=
    host:host         {: RESULT = new HostValue(host.getValue()); :}
  | host_set:hostSet  {: RESULT = hostSet; :}
  | value:value       {: RESULT = value; :}
  ;
