process main {
  /* Round 1 */
  int{A} a1 <- recv @alice;
  int{B} b1 <- recv @bob;

  int a1_endorsed = endorse(a1, {A & (B & C)<-});
  int b1_endorsed = endorse(b1, {B & (A & C)<-});

  /* Reveal who had the higher bid. */
  bool a1_higher = declassify(a1_endorsed > b1_endorsed, {A ⊓ B ⊓ C});
  send a1_higher to @alice;
  send a1_higher to @bob;

  /* Round 2 */
  int{A} a2 <- recv @alice;
  int{B} b2 <- recv @bob;

  /* Reveal the overall winner. */
  int a2_endorsed = endorse(a2, {A & (B & C)<-});
  int b2_endorsed = endorse(b2, {B & (A & C)<-});

  int a_sum = a1_endorsed + a2_endorsed;
  int b_sum = b1_endorsed + b2_endorsed;

  bool a_higher = declassify(a_sum > b_sum, {A ⊓ B ⊓ C});
  send a_higher to @alice;
  send a_higher to @bob;
  send a_higher to @chuck;
}


process @alice {
  /* First bid. */
  send 10 to main;

  /* Adjust bid based on result of Round 1. */
  bool a1_higher <- recv main;
  if (a1_higher) {
    send 5 to main;
  } else {
    send 20 to main;
  }

  /* See who won. */
  bool a_higher <- recv main;
  assert a_higher;
}

process @bob {
  /* First bid. */
  send 20 to main;

  /* Adjust bid based on result of Round 1. */
  bool a1_higher <- recv main;
  if (a1_higher) {
    send 80 to main;
  } else {
    send 5 to main;
  }

  /* See who won. */
  bool a_higher <- recv main;
  assert a_higher;
}

host alice : {A};
host bob   : {B};
host chuck : {C};
