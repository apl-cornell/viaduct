import io.github.aplcornell.viaduct.errors.NoMainError;
import io.github.aplcornell.viaduct.errors.ParsingError;
import io.github.aplcornell.viaduct.security.*;
import io.github.aplcornell.viaduct.syntax.*;
import io.github.aplcornell.viaduct.syntax.datatypes.*;
import io.github.aplcornell.viaduct.syntax.surface.*;
import io.github.aplcornell.viaduct.syntax.operators.*;
import io.github.aplcornell.viaduct.syntax.types.*;
import io.github.aplcornell.viaduct.syntax.values.*;

import java.util.List;
import java.util.LinkedList;
import java.util.Map;
import kotlin.collections.CollectionsKt;
import kotlin.Pair;

import java_cup.runtime.ComplexSymbolFactory;
import java_cup.runtime.ComplexSymbolFactory.Location;
import java_cup.runtime.Symbol;


action code {:
    /** Constructs a binary expression node. Source location is inferred from the arguments. */
    private OperatorApplicationNode binaryExpression(BinaryOperator op, ExpressionNode lhs, ExpressionNode rhs) {
        Arguments arguments = Arguments.from(lhs, rhs);
        SourceRange sourceLocation = lhs.getSourceLocation().merge(rhs.getSourceLocation());
        return new OperatorApplicationNode(op, arguments, sourceLocation);
    }

    /** Constructs a label argument list from a single optional label. */
    private Arguments<Located<LabelExpression>> optionalLabelArgument(Located<LabelExpression> label) {
        if (label == null) {
            return null;
        } else {
            return Arguments.from(label);
        }
    }
:}

parser code {:
    Map<ProtocolName, ProtocolParser<Protocol>> protocolParsers;

    /** Generates source location information for a node given left and right character offsets. */
    private SourceRange location(int left, int right) {
        return ((Lexer) getScanner()).location(left, right);
    }

    @Override
    public void report_error(String message, Object info) {
        report_fatal_error(message, info);
    }

    @Override
    public void report_fatal_error(String message, Object info) {
        done_parsing();

        final Symbol infoSymbol = (Symbol) info;
        final SourceRange errorLocation = location(infoSymbol.left, infoSymbol.right);
        final String actualToken = symbl_name_from_id(infoSymbol.sym);
        final List<String> expectedTokens = new LinkedList<>();
        for (Integer id : expected_token_ids()) {
            expectedTokens.add(symbl_name_from_id(id));
        }
        throw new ParsingError(errorLocation, actualToken, expectedTokens);
    }


    // Used to support multiple start symbols.
    private Symbol current_symbol;

    private void set_current_symbol(int symbol) {
        final Location location = new Location(0, 0, 0);
        current_symbol =
            ((ComplexSymbolFactory) getSymbolFactory())
                .newSymbol(symbl_name_from_id(symbol), symbol, location, location);
    }

    /** Parses the input as a program. */
    public ProgramNode parseProgram() throws Exception {
        set_current_symbol(sym.PARSE_PROGRAM);
        return (ProgramNode) parse().value;
    }

    /** Parses the input as a security label. */
    public LabelExpression parseLabel() throws Exception {
        set_current_symbol(sym.PARSE_LABEL);
        return (LabelExpression) parse().value;
    }
:}

scan with {:
    if (current_symbol != null) {
        final Symbol result = current_symbol;
        current_symbol = null;
        return result;
    }
    return getScanner().next_token();
:};

nonterminal ProgramNode program;
nonterminal List<TopLevelDeclarationNode> declaration_list;
nonterminal TopLevelDeclarationNode declaration;

nonterminal SimpleStatementNode simple_stmt;
nonterminal ExpressionNode optional_initial_value;
nonterminal BinaryOperator compound_assignment;
nonterminal StatementNode stmt;
nonterminal BlockNode block;
nonterminal List<StatementNode> stmt_list;

nonterminal ExpressionNode expr;
nonterminal List<ExpressionNode> expr_list, nonempty_expr_list;
nonterminal Located<ArgumentLabel> argument_label;
nonterminal IndexingNode indexing;

nonterminal Value value;
nonterminal Located<ValueType> value_type;
nonterminal Located<LabelExpression> label, optional_label, optional_from_label, optional_to_label;
nonterminal Located<LabelExpression> label_signature, optional_label_signature, optional_pc;
nonterminal LabelExpression principal_expr, label_expr, label_expr_noparam, label_expr_signature;

nonterminal Located<ObjectVariable> variable;
nonterminal Located<Host> host;
// TODO: the above type causes a parse error in the IntelliJ plugin
nonterminal List host_list, nonempty_host_list;
nonterminal HostSetValue host_set;

nonterminal List parameter_list, nonempty_parameter_list;
nonterminal ParameterNode parameter;
nonterminal ParameterDirection parameter_direction;

nonterminal List argument_list, nonempty_argument_list;
nonterminal FunctionArgumentNode argument;

nonterminal Located<Protocol> protocol;
nonterminal Located<Protocol> protocol_annot;
nonterminal Located<ProtocolName> protocol_name;
// TODO: more accurate types cause parse errors in IntelliJ

nonterminal List protocol_argument_list, nonempty_protocol_argument_list;
nonterminal Pair<Located, Located> protocol_labelled_argument;
nonterminal Value protocol_argument;

nonterminal DelegationDeclarationNode ifc_delegation;
nonterminal Located label_param;
nonterminal List optional_polymorphic_label_param_list, polymorphic_label_param_list, nonempty_polymorphic_label_param_list;
nonterminal List optional_ifc_delegation_list, ifc_delegation_list, nonempty_ifc_delegation_list;
nonterminal DelegationProjection projection;

terminal String IDENT, CAP_IDENT;
terminal String TMP_IDENT;
terminal int INT_LIT;
terminal TRUE, FALSE;

terminal HOST;
terminal FUNCTION;
terminal OUT;
terminal COLON;

terminal TRUSTS, ASSUME;

terminal EQ;
terminal PLUSEQ, MINUSEQ, TIMESEQ, DIVIDEEQ;
terminal LET, VAL, VAR;
terminal IF, ELSE, WHILE, FOR, LOOP, BREAK;
terminal INPUT, FROM, OUTPUT, TO;
terminal ASSERT;

terminal NOT, ANDAND, OROR;
terminal EQEQ, NEQ, LT, LEQ, GT, GEQ;
terminal PLUS, MINUS, TIMES, DIVIDE;
terminal MIN, MAX, MUX;

terminal FLOWSTO;
terminal WHERE;

terminal DECLASSIFY, ENDORSE, DOWNGRADE;

terminal INT, BOOL, UNIT, ARRAY;
terminal TOP, BOTTOM, OR, AND, JOIN, MEET, RARROW, LARROW;
terminal AT;

terminal OPEN_BRACE, CLOSE_BRACE, SEMICOLON;
terminal OPEN_SQBRACE, CLOSE_SQBRACE;
terminal OPEN_PAREN, CLOSE_PAREN, COMMA;

// Used to support multiple start symbols
nonterminal entry_point;
terminal Symbol PARSE_PROGRAM, PARSE_LABEL;


precedence left ANDAND, OROR, AND, OR, JOIN, MEET;
precedence nonassoc EQEQ, NEQ, LT, LEQ, GT, GEQ;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence right NOT, MIN;
precedence left LARROW, RARROW;
precedence nonassoc OPEN_PAREN, CLOSE_PAREN;

start with entry_point;

entry_point ::=
     PARSE_PROGRAM program:s    {: RESULT = s; :}
  |  PARSE_LABEL   label_expr:s {: RESULT = s; :}
  ;

program ::=
    declaration_list:declarations {:
      if (declarations.isEmpty()) {
        /* Empty program */
        throw new NoMainError(((Lexer) getScanner()).sourceFile.getPath());
      }


      RESULT = new ProgramNode(declarations, location(declarationsleft, declarationsright));
    :}
  ;

declaration_list ::=
    declaration_list:declarations declaration:declaration {:
      declarations.add(declaration);
      RESULT = declarations;
    :}

  | /* empty */ {:
      RESULT = CollectionsKt.mutableListOf();
    :}
  ;

declaration ::=
  FUNCTION:begin IDENT:funcname optional_polymorphic_label_param_list:polylabels
    OPEN_PAREN:paramsbegin parameter_list:params CLOSE_PAREN:paramsclose
    optional_ifc_delegation_list:assumptions optional_pc:label block:body {:
      Arguments polylabelsarg = null;
      if (polylabels != null) polylabelsarg = new Arguments(polylabels, location(funcnameleft, paramsbeginleft));
      Arguments assumptionsarg = null;
      if (assumptions != null) assumptionsarg = new Arguments(assumptions, location(paramscloseleft, labelleft));
      RESULT = new FunctionDeclarationNode(
        new Located(new FunctionName(funcname), location(funcnameleft, funcnameright)),
        polylabelsarg,
        new Arguments(params, location(paramsbeginleft, paramsbeginright)),
        assumptionsarg,
        label,
        body,
        location(beginleft, bodyright),
        null
      );
  :}
  | HOST:begin IDENT:host {:
      RESULT = new HostDeclarationNode(
        new Located(new Host(host), location(hostleft, hostright)),
        location(beginleft, hostright), null);
    :}
  /*| ASSUME:begin FOR RARROW principal_expr:label1 TRUSTS principal_expr:label2 {:
      RESULT = new AuthorityDelegationDeclarationNode(
        new Located(label1, location(label1left, label1right)),
        new Located(label2, location(label2left, label2right)),
        DelegationProjection.CONFIDENTIALITY,
        location(beginleft, label2right), null);
  :}
  | ASSUME:begin FOR LARROW principal_expr:label1 TRUSTS principal_expr:label2 {:
      RESULT = new AuthorityDelegationDeclarationNode(
        new Located(label1, location(label1left, label1right)),
        new Located(label2, location(label2left, label2right)),
        DelegationProjection.INTEGRITY,
        location(beginleft, label2right), null);
  :}*/
  | ASSUME:begin projection: proj label_expr:label1 TRUSTS label_expr:label2 {:
      RESULT = new AuthorityDelegationDeclarationNode(
        new Located(label1, location(label1left, label1right)),
        new Located(label2, location(label2left, label2right)),
        proj,
        location(beginleft, label2right), null);
  :}
  ;


projection ::=
  FOR RARROW {:
    RESULT = DelegationProjection.CONFIDENTIALITY;
  :}
  | FOR LARROW {:
    RESULT = DelegationProjection.INTEGRITY;
  :}
  | /* empty */ {:
    RESULT = DelegationProjection.BOTH;
  :}
  ;

optional_pc ::=
  COLON label_signature:label {:
    RESULT = label;
  :}

  | /* empty */ {:
      RESULT = null;
    :}
  ;

//delegation syntax for polymorphic label list
optional_ifc_delegation_list ::=
   WHERE OPEN_PAREN ifc_delegation_list:delegations CLOSE_PAREN {:
      RESULT = delegations;
    :}

  | /* empty */ {:
      RESULT = null;
    :}
  ;

ifc_delegation_list ::=
    nonempty_ifc_delegation_list:delegations {:
      RESULT = delegations;
    :}

  | /* empty */ {:
      RESULT = null;
    :}
  ;
nonempty_ifc_delegation_list ::=
    nonempty_ifc_delegation_list:delegations COMMA ifc_delegation:delegation {:
      delegations.add(delegation);
      RESULT = delegations;
    :}
  | ifc_delegation:delegation {:
      RESULT = CollectionsKt.mutableListOf(delegation);
    :}
    ;

ifc_delegation ::=
  label_expr:label1 FLOWSTO label_expr:label2 {:
      RESULT = new IFCDelegationDeclarationNode(
        new Located(label1, location(label1left, label1right)),
        new Located(label2, location(label2left, label2right)),
        DelegationProjection.BOTH, location(label1left, label2right), null);
  :}
  ;

optional_polymorphic_label_param_list ::=
OPEN_BRACE:polybegin polymorphic_label_param_list:polylabels CLOSE_BRACE:polyend {:
      RESULT = polylabels;
    :}

  | /* empty */ {:
      RESULT = null;
    :}
  ;
polymorphic_label_param_list ::=
    nonempty_polymorphic_label_param_list:labels {:
      RESULT = labels;
    :}

  | /* empty */ {:
      RESULT = null;
    :}
  ;

nonempty_polymorphic_label_param_list ::=
    nonempty_polymorphic_label_param_list: params COMMA label_param: label {:
      params.add(label);
      RESULT = params;
    :}
  | label_param:label {:
      RESULT = CollectionsKt.mutableListOf(label);
    :}
    ;

label_param ::=
  CAP_IDENT:label {:
    RESULT = new Located(new LabelVariable(label), location(labelleft, labelright));
  :}
  ;

parameter_list ::=
    nonempty_parameter_list:params {:
      RESULT = params;
    :}

  | /* empty */ {:
      RESULT = CollectionsKt.mutableListOf();
    :}
  ;

nonempty_parameter_list ::=
    nonempty_parameter_list:params COMMA parameter:param {:
      params.add(param);
      RESULT = params;
    :}

  | parameter:param {:
      RESULT = CollectionsKt.mutableListOf(param);
    :}
  ;

parameter_direction ::=
    OUT         {: RESULT = ParameterDirection.OUT; :}
  | /* empty */ {: RESULT = ParameterDirection.IN; :}
  ;

parameter ::=
    variable:name COLON value_type:type optional_label_signature:label protocol_annot:protocol {:
      RESULT = new ParameterNode(
        name,
        ParameterDirection.IN,
        new ObjectTypeNode(
          new Located(PrimitivesKt.getImmutableCell(), name.getSourceLocation()),
          Arguments.from(type),
          optionalLabelArgument(label)
        ),
        protocol,
        location(nameleft, labelright),
        null
      );
    :}

  | variable:name COLON OUT value_type:type label_signature:label protocol_annot:protocol {:
      RESULT = new ParameterNode(
        name,
        ParameterDirection.OUT,
        new ObjectTypeNode(
          new Located(PrimitivesKt.getImmutableCell(), name.getSourceLocation()),
          Arguments.from(type),
          optionalLabelArgument(label)
        ),
        protocol,
        location(nameleft, labelright),
        null
      );
    :}

  | variable:name COLON ARRAY OPEN_SQBRACE value_type:elementType CLOSE_SQBRACE
    optional_label_signature:label protocol_annot:protocol {:
      RESULT = new ParameterNode(
        name,
        ParameterDirection.IN,
        new ObjectTypeNode(
          new Located(PrimitivesKt.getVector(), name.getSourceLocation()),
          Arguments.from(elementType),
          optionalLabelArgument(label)
        ),
        protocol,
        location(nameleft, labelright),
        null
      );
    :}

  | variable:name COLON OUT ARRAY OPEN_SQBRACE value_type:elementType CLOSE_SQBRACE
    label_signature:label protocol_annot:protocol {:
      RESULT = new ParameterNode(
        name,
        ParameterDirection.OUT,
        new ObjectTypeNode(
          new Located(PrimitivesKt.getVector(), name.getSourceLocation()),
          Arguments.from(elementType),
          optionalLabelArgument(label)
        ),
        protocol,
        location(nameleft, labelright),
        null
      );
    :}
  ;

simple_stmt ::=
    /* Immutable variable declaration. */
    VAL:begin variable:var COLON value_type:type optional_label:label protocol_annot:protocol EQ expr:value {:
      RESULT = new DeclarationNode(
        var,
        new ConstructorCallNode(
            new ObjectTypeNode(
              new Located(PrimitivesKt.getImmutableCell(), var.getSourceLocation()),
              Arguments.from(type),
              optionalLabelArgument(label)
            ),
            protocol,
            Arguments.from(value),
            location(typeleft, valueright)
        ),
        location(beginleft, valueright),
        null
      );
    :}

    /* Mutable variable declaration. */
  | VAR:begin variable:var COLON value_type:type optional_label:label protocol_annot:protocol optional_initial_value:initial {:
      final LiteralNode defaultValue = new LiteralNode(
        type.getValue().getDefaultValue(),
        type.getSourceLocation());

      RESULT = new DeclarationNode(
        var,
        new ConstructorCallNode(
            new ObjectTypeNode(
              new Located(PrimitivesKt.getMutableCell(), var.getSourceLocation()),
              Arguments.from(type),
              optionalLabelArgument(label)
            ),
            protocol,
            Arguments.from((initial == null) ? defaultValue : initial),
            location(typeleft, initialright)
        ),
        location(beginleft, initialright),
        null
      );
    :}

    /* Generic object declaration. */
  | VAL:begin variable:var EQ expr:e {:
      RESULT = new DeclarationNode(
        var,
        e,
        location(beginleft, eright),
        null
      );
    :}

  | indexing:indexing EQ:eq expr:e {:
      RESULT = indexing.set(e, location(eqleft, eqright));
    :}

  | indexing:indexing compound_assignment:op expr:e {:
      RESULT = indexing.modify(op, e, location(opleft, opright));
        :}

  /* out parameter declaration */
  | OUT:begin IDENT:id EQ expr:e {:
      RESULT = new OutParameterInitializationNode(
        new Located(new ObjectVariable(id), location(idleft, idright)),
        e,
        location(beginleft, eright),
        null
      );
    :}

  /* function call */
  | IDENT:funcname OPEN_PAREN:paramsopen argument_list:args CLOSE_PAREN:end {:
      RESULT = new FunctionCallNode(
        new Located(new FunctionName(funcname), location(funcnameleft, funcnameright)),
        new Arguments(args, location(paramsopenleft, endright)),
        location(funcnameleft, endright),
        null
      );
    :}

  /* let binding */
  | LET:begin TMP_IDENT:id protocol_annot:protocol EQ expr:e {:
      RESULT = new LetNode(
        new Located(new Temporary(id), location(idleft, idright)),
        e,
        protocol,
        location(beginleft, eright),
        null
      );
    :}

  | OUTPUT:begin expr:message TO host:recipient {:
      RESULT = new OutputNode(message, recipient, location(beginleft, recipientright), null);
    :}
    // delegation syntax
  ;

optional_initial_value ::=
    EQ expr:e    {: RESULT = e; :}
  | /* empty */  {: RESULT = null; :}
  ;

compound_assignment ::=
    PLUSEQ   {: RESULT = Addition.INSTANCE; :}
  | MINUSEQ  {: RESULT = Subtraction.INSTANCE; :}
  | TIMESEQ  {: RESULT = Multiplication.INSTANCE; :}
  | DIVIDEEQ {: RESULT = Division.INSTANCE; :}
  ;

argument ::=
    expr:expr {:
      RESULT = new ExpressionArgumentNode(expr, expr.getSourceLocation());
    :}

  | AND:begin variable:var {:
      RESULT = new ObjectReferenceArgumentNode(var, location(beginleft, varright));
    :}

  | VAL:begin variable:var {:
      RESULT = new ObjectDeclarationArgumentNode(var, location(beginleft, varright));
    :}

  | OUT:begin variable:var {:
      RESULT = new OutParameterArgumentNode(var, location(beginleft, varright));
    :}
  ;

nonempty_argument_list ::=
    nonempty_argument_list:args COMMA argument:arg {:
      args.add(arg);
      RESULT = args;
    :}

  | argument:arg {:
      RESULT = CollectionsKt.mutableListOf(arg);
    :}
  ;

argument_list ::=
    nonempty_argument_list:args {:
      RESULT = args;
    :}

  | /* empty */ {:
      RESULT = CollectionsKt.mutableListOf();
    :}
  ;

stmt ::=
    simple_stmt:stmt SEMICOLON {:
      RESULT = stmt;
    :}

  | IF:begin OPEN_PAREN expr:guard CLOSE_PAREN block:thenCase ELSE block:elseCase {:
      RESULT = new IfNode(guard, thenCase, elseCase, location(beginleft, elseCaseright), null);
    :}

  | IF:begin OPEN_PAREN expr:guard CLOSE_PAREN block:thenCase {:
      SourceRange sourceLocation = location(beginleft, thenCaseright);
      RESULT = new IfNode(
        guard,
        thenCase,
        new BlockNode(CollectionsKt.listOf(), sourceLocation, null),
        sourceLocation,
        null
      );
    :}

  | WHILE:begin OPEN_PAREN expr:guard CLOSE_PAREN block:body {:
      RESULT = new WhileLoopNode(guard, body, null, location(beginleft, bodyright), null);
    :}

  | FOR:begin OPEN_PAREN simple_stmt:initialize SEMICOLON expr:guard SEMICOLON simple_stmt:update CLOSE_PAREN block:body {:
      RESULT = new ForLoopNode(initialize, guard, update, body, null, location(beginleft, bodyright), null);
    :}

  | LOOP:begin block:body {:
      RESULT = new InfiniteLoopNode(body, null, location(beginleft, bodyright), null);
    :}

  | BREAK:brk SEMICOLON {:
      RESULT = new BreakNode(null, location(brkleft, brkright), null);
    :}

  | ASSERT:begin expr:e SEMICOLON {:
      RESULT = new AssertionNode(e, location(beginleft, eright), null);
    :}

  | block:block {:
      RESULT = block;
    :}
  ;

block ::=
    OPEN_BRACE:begin stmt_list:statements CLOSE_BRACE:end {:
      RESULT = new BlockNode(statements, location(beginleft, endright), null);
    :}
  ;

stmt_list ::=
    stmt_list:slist stmt:s {:
      slist.add(s);
      RESULT = slist;
    :}

  | /* empty */ {:
      RESULT = CollectionsKt.mutableListOf();
    :}
  ;


expr ::=
    value:value {:
      RESULT = new LiteralNode(value, location(valueleft, valueright));
    :}

  | indexing:indexing {:
       RESULT = indexing.get();
    :}

  | TMP_IDENT:id {:
      RESULT = new ReadNode(new Located(new Temporary(id), location(idleft, idright)));
    :}

  | NOT:begin expr:e {:
      RESULT = new OperatorApplicationNode(
        Not.INSTANCE,
        Arguments.from(e),
        location(beginleft, eright));
    :}

  | MINUS:begin expr:e {:
      RESULT = new OperatorApplicationNode(
        Negation.INSTANCE,
        Arguments.from(e),
        location(beginleft, eright));
    :}

  | expr:e1 ANDAND expr:e2 {:
      RESULT = binaryExpression(And.INSTANCE, e1, e2);
    :}

  | expr:e1 OROR expr:e2 {:
      RESULT = binaryExpression(Or.INSTANCE, e1, e2);
    :}

  | expr:e1 AND expr:e2 {:
      RESULT = binaryExpression(ExclusiveOr.INSTANCE, e1, e2);
    :}

  | expr:e1 EQEQ expr:e2 {:
      RESULT = binaryExpression(EqualTo.INSTANCE, e1, e2);
    :}

  | expr:e1 NEQ expr:e2 {:
      RESULT = binaryExpression(ExclusiveOr.INSTANCE, e1, e2);
    :}

  | expr:e1 LT expr:e2 {:
      RESULT = binaryExpression(LessThan.INSTANCE, e1, e2);
    :}

  | expr:e1 LEQ expr:e2 {:
      RESULT = binaryExpression(LessThanOrEqualTo.INSTANCE, e1, e2);
    :}

  | expr:e1 GT expr:e2 {:
      RESULT = binaryExpression(GreaterThan.INSTANCE, e1, e2);
    :}

  | expr:e1 GEQ expr:e2 {:
      RESULT = binaryExpression(GreaterThanOrEqualTo.INSTANCE, e1, e2);
    :}

  | expr:e1 PLUS expr:e2 {:
      RESULT = binaryExpression(Addition.INSTANCE, e1, e2);
    :}

  | expr:e1 MINUS expr:e2 {:
      RESULT = binaryExpression(Subtraction.INSTANCE, e1, e2);
    :}

  | expr:e1 TIMES expr:e2 {:
      RESULT = binaryExpression(Multiplication.INSTANCE, e1, e2);
    :}

  | expr:e1 DIVIDE expr:e2 {:
      RESULT = binaryExpression(Division.INSTANCE, e1, e2);
    :}

  | MIN:begin OPEN_PAREN expr:e1 COMMA expr:e2 CLOSE_PAREN:end {:
      RESULT = binaryExpression(Minimum.INSTANCE, e1, e2);
    :}

  | MAX:begin OPEN_PAREN expr:e1 COMMA expr:e2 CLOSE_PAREN:end {:
      RESULT = binaryExpression(Maximum.INSTANCE, e1, e2);
    :}

  | MUX:begin OPEN_PAREN expr:e1 COMMA expr:e2 COMMA expr:e3 CLOSE_PAREN:end {:
      Arguments arguments = Arguments.from(e1, e2, e3);
      SourceRange sourceLocation = e1.getSourceLocation().merge(e2.getSourceLocation()).merge(e3.getSourceLocation());
      RESULT = new OperatorApplicationNode(Mux.INSTANCE, arguments, location(beginleft, endright));
    :}

  | DECLASSIFY:begin expr:e optional_from_label:from TO label:to {:
      RESULT = new DeclassificationNode(e, from, to, location(beginleft, toright));
    :}

    // invert FROM and TO labels to prevent shift-reduce conflict
  | ENDORSE:begin expr:e optional_to_label:to FROM label:from {:
      RESULT = new EndorsementNode(e, from, to, location(beginleft, fromright));
    :}

  | INPUT:begin value_type:type FROM host:sender {:
      RESULT = new InputNode(type, sender, location(beginleft, senderright));
    :}

  | OPEN_PAREN expr:e CLOSE_PAREN {:
      RESULT = e;
    :}

  // constructor call, make this general later
  | ARRAY:begin OPEN_SQBRACE value_type:elementType CLOSE_SQBRACE
    optional_label:label protocol_annot:protocol OPEN_PAREN expr:size CLOSE_PAREN:end
    {:
      RESULT = new ConstructorCallNode(
        new ObjectTypeNode(
          new Located(PrimitivesKt.getVector(), location(beginleft, beginright)),
          Arguments.from(elementType),
          optionalLabelArgument(label)
        ),
        protocol,
        Arguments.from(size),
        location(beginleft, endright)
      );
    :}
  ;

expr_list ::=
    nonempty_expr_list:elist {:
      RESULT = elist;
    :}

  | /* empty */ {:
      RESULT = CollectionsKt.listOf();
    :}
  ;

nonempty_expr_list ::=
    nonempty_expr_list:elist COMMA expr:e {:
      elist.add(e);
      RESULT = elist;
    :}

  | expr:e {:
      RESULT = CollectionsKt.mutableListOf(e);
    :}
  ;

argument_label ::=
    IDENT:label {:
      RESULT = new Located(new ArgumentLabel(label), location(labelleft, labelright));
    :}
  /* Parse the "host" keyword as a label also. */
  // TODO: find a more generic solution (lexer states?)
  | HOST:label {:
      RESULT = new Located(new ArgumentLabel("host"), location(labelleft, labelright));
    :}
  ;

indexing ::=
    variable:var {:
      RESULT = new IndexingNode(var, new Arguments(location(varleft, varright)));
    :}

  | variable:var OPEN_SQBRACE:open nonempty_expr_list:indices CLOSE_SQBRACE:close {:
      RESULT = new IndexingNode(var, new Arguments(indices, location(openleft, closeright)));
    :}
  ;


value ::=
    TRUE {:
      RESULT = new BooleanValue(true);
    :}

  | FALSE {:
      RESULT = new BooleanValue(false);
    :}

  | INT_LIT:literal {:
      RESULT = new IntegerValue(literal);
    :}

  | UNIT {:
      RESULT = UnitValue.INSTANCE;
    :}
  ;


value_type ::=
    INT:type {:
      RESULT = new Located(IntegerType.INSTANCE, location(typeleft, typeright));
    :}

  | BOOL:type {:
      RESULT = new Located(BooleanType.INSTANCE, location(typeleft, typeright));
    :}

  | UNIT:type {:
      RESULT = new Located(UnitType.INSTANCE, location(typeleft, typeright));
    :}
  ;

label ::=
    OPEN_BRACE:begin label_expr:l CLOSE_BRACE:end {:
      RESULT = new Located(l, location(beginleft, endright));
    :}
  ;

principal_expr ::=
    IDENT:id {: RESULT = new LabelLiteral(new Host(id)); :}

  | CAP_IDENT:id {: RESULT = new LabelParameter(new LabelVariable(id)); :}

  | TOP {: RESULT = LabelTop.INSTANCE; :}

  | BOTTOM {: RESULT = LabelBottom.INSTANCE; :}

  | principal_expr:l1 OR principal_expr:l2 {:
      RESULT = new LabelOr(l1, l2);
    :}

  | principal_expr:l1 AND principal_expr:l2 {:
      RESULT = new LabelAnd(l1, l2);
    :}

  | principal_expr:l1 JOIN principal_expr:l2 {:
      RESULT = new LabelJoin(l1, l2);
    :}

  | principal_expr:l1 MEET principal_expr:l2 {:
      RESULT = new LabelMeet(l1, l2);
    :}

  | OPEN_PAREN principal_expr:l CLOSE_PAREN {:
      RESULT = l;
    :}
  ;

label_expr ::=
    IDENT:id {: RESULT = new LabelLiteral(new Host(id)); :}

  | CAP_IDENT:id {: RESULT = new LabelParameter(new LabelVariable(id)); :}

  | TOP {: RESULT = LabelTop.INSTANCE; :}

  | BOTTOM {: RESULT = LabelBottom.INSTANCE; :}

  | label_expr:l1 OR label_expr:l2 {:
      RESULT = new LabelOr(l1, l2);
    :}

  | label_expr:l1 AND label_expr:l2 {:
      RESULT = new LabelAnd(l1, l2);
    :}

  | label_expr:l1 JOIN label_expr:l2 {:
      RESULT = new LabelJoin(l1, l2);
    :}

  | label_expr:l1 MEET label_expr:l2 {:
      RESULT = new LabelMeet(l1, l2);
    :}

  | label_expr:l RARROW {:
      RESULT = new LabelConfidentiality(l);
    :}

  | label_expr:l LARROW {:
      RESULT = new LabelIntegrity(l);
    :}

  | OPEN_PAREN label_expr:l CLOSE_PAREN {:
      RESULT = l;
    :}
  ;

label_signature ::=
    OPEN_BRACE:begin label_expr_signature:l CLOSE_BRACE:end {:
      RESULT = new Located(l, location(beginleft, endright));
    :}
  ;

label_expr_signature ::=
    label_expr_noparam:l {: RESULT = l; :}

  | CAP_IDENT:id             {: RESULT = new LabelParameter(new LabelVariable(id)); :}
  ;

label_expr_noparam ::=
    IDENT:id {: RESULT = new LabelLiteral(new Host(id)); :}

  | TOP {: RESULT = LabelTop.INSTANCE; :}

  | BOTTOM {: RESULT = LabelBottom.INSTANCE; :}

  | label_expr:l1 OR label_expr:l2 {:
      RESULT = new LabelOr(l1, l2);
    :}

  | label_expr:l1 AND label_expr:l2 {:
      RESULT = new LabelAnd(l1, l2);
    :}

  | label_expr:l1 JOIN label_expr:l2 {:
      RESULT = new LabelJoin(l1, l2);
    :}

  | label_expr:l1 MEET label_expr:l2 {:
      RESULT = new LabelMeet(l1, l2);
    :}

  | label_expr:l RARROW {:
      RESULT = new LabelConfidentiality(l);
    :}

  | label_expr:l LARROW {:
      RESULT = new LabelIntegrity(l);
    :}

  | OPEN_PAREN label_expr:l CLOSE_PAREN {:
      RESULT = l;
    :}
  ;

optional_label ::=
    label:l     {: RESULT = l; :}
  | /* empty */ {: RESULT = null; :}
  ;

optional_label_signature ::=
    label_signature:l     {: RESULT = l; :}
  | /* empty */         {: RESULT = null; :}
  ;

optional_from_label ::=
    FROM label:l {: RESULT = l; :}
  | /* empty */  {: RESULT = null; :}
  ;

optional_to_label ::=
    TO label:l {: RESULT = l; :}
  | /* empty */  {: RESULT = null; :}
  ;


variable ::=
    IDENT:id  {:
      RESULT = new Located(new ObjectVariable(id), location(idleft, idright));
    :}
  ;

host ::=
    IDENT:id {:
      RESULT = new Located(new Host(id), location(idleft, idright));
    :}
  ;

host_list ::=
    nonempty_host_list:hosts {:
      RESULT = hosts;
    :}

  | /* empty */ {:
      RESULT = CollectionsKt.mutableListOf();
    :}
  ;

nonempty_host_list ::=
    nonempty_host_list:hosts COMMA host:host {:
      hosts.add(host);
      RESULT = hosts;
    :}

  | host:host {:
      RESULT = CollectionsKt.mutableListOf(host);
    :}
  ;

host_set ::=
    OPEN_BRACE host_list:hosts CLOSE_BRACE {:
      RESULT = HostSetValue.invoke(hosts);
    :}
  ;

protocol_annot ::=
    AT protocol:p     {: RESULT = p; :}
  | /* empty */       {: RESULT = null; :}
  ;

protocol ::=
    protocol_name:protocolName OPEN_PAREN:open protocol_argument_list:arguments CLOSE_PAREN:close {:
      RESULT = new Located(
        ProtocolParserKt.parseProtocol(
          protocolParsers,
          protocolName,
          NamedArguments.invoke(arguments, location(openleft, closeright))),
        location(protocolNameleft, closeright));
    :}
  ;

protocol_name ::=
    CAP_IDENT:protocolName {:
      RESULT = new Located(new ProtocolName(protocolName), location(protocolNameleft, protocolNameright));
    :}
  ;

protocol_argument_list ::=
    nonempty_protocol_argument_list:arguments {:
      RESULT = arguments;
    :}

  | /* empty */ {:
      RESULT = CollectionsKt.listOf();
    :}
  ;

nonempty_protocol_argument_list ::=
    nonempty_protocol_argument_list:arguments COMMA protocol_labelled_argument:argument {:
      arguments.add(argument);
      RESULT = arguments;
    :}

  | protocol_labelled_argument:argument {:
      RESULT = CollectionsKt.mutableListOf(argument);
    :}
  ;

protocol_labelled_argument ::=
    argument_label:label EQ protocol_argument:value {:
      RESULT = new Pair(
        label,
        new Located(value, location(valueleft, valueright))
      );
    :}
  ;

protocol_argument ::=
    host:host         {: RESULT = new HostValue(host.getValue()); :}
  | host_set:hostSet  {: RESULT = hostSet; :}
  | value:value       {: RESULT = value; :}
  ;
