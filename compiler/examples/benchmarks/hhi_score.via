/*
 * Calculates the Herfindahl-Hirschman index for market competitiveness.
 * In this scenario, we're assessing the competitiveness of the widget market
 * amongst two widget makers by calculating the revenue of each company
 * (by summing up sales in n stores) and then calculating each company's
 * market share and applying the HHI formula.
 */
process main {
    val storeCount: int = 100;

    val a = Array[int]{A}(storeCount);
    for (var i : int = 0; i < storeCount; i += 1) {
        a[i] = input int from companyA;
    }

    val b = Array[int]{B}(storeCount);
    for (var i : int = 0; i < storeCount; i += 1) {
        b[i] = input int from companyB;
    }

    var a_rev : int{A} = 0;
    for (var i : int = 0; i < storeCount; i += 1) {
        a_rev += a[i];
    }

    var b_rev : int{B} = 0;
    for (var i : int = 0; i < storeCount; i += 1) {
        b_rev += b[i];
    }

    val a_rev_trusted : int = endorse a_rev to {A & B<-};
    val b_rev_trusted : int = endorse b_rev to {B & A<-};
    val total_market : int = a_rev_trusted + b_rev_trusted;

    /*
    ABY doesn't support division so comment this out for now
    val a_share : int = (100 * a_rev_trusted) / total_market;
    val b_share : int = (100 * b_rev_trusted) / total_market;
    */

    val a_share : int = 100 * a_rev_trusted;
    val b_share : int = 100 * b_rev_trusted;

    val hhi : int = declassify (a_share * a_share + b_share * b_share) to {A âŠ“ B};

    output hhi to companyA;
    output hhi to companyB;
}

host companyA  : {A}
host companyB  : {B}
