host alice
host bob

fun main() {
  val num_turns: int{alice ⊓ bob} = 3;
  var a_score: int{alice ⊓ bob} = 0;
  var b_score: int{alice ⊓ bob} = 0;
  val a_moves = Array[int]{alice & bob<-}(num_turns);
  val b_moves = Array[int]{bob & alice<-}(num_turns);

  for (var i: int{alice ⊓ bob} = 0; i < num_turns; i += 1) {
    a_moves[i] = endorse (input int from alice) to {alice & bob<-} from {alice};
    b_moves[i] = endorse (input int from bob) to {bob & alice<-} from {bob};
  }

  for (var turn: int{alice ⊓ bob} = 0; turn < num_turns; turn += 1) {
    val a_move: int{alice & bob<-} = a_moves[turn];
    val b_move: int{bob & alice<-} = b_moves[turn];

    val a_move_public: int{alice ⊓ bob} = declassify a_move to {alice ⊓ bob};
    val b_move_public: int{alice ⊓ bob} = declassify b_move to {alice ⊓ bob};

    /* 1 = rock; 2 = paper; 3 = scissors; */
    val a_valid: bool{alice ⊓ bob} = 1 <= a_move_public && a_move_public <= 3;
    val b_valid: bool{alice ⊓ bob} = 1 <= b_move_public && b_move_public <= 3;

    /* alice cheats */
    if (!a_valid && b_valid) {
      b_score += 1;
    }

    /* bob cheats */
    if (a_valid && !b_valid) {
      a_score += 1;
    }

    /* neither cheat */
    if (a_valid && b_valid) {
      if (a_move_public < b_move_public && b_move_public < 3) {
        b_score += 1;
      }

      if (b_move_public < a_move_public && a_move_public < 3) {
        a_score += 1;
      }

      if (a_move_public == 1 && b_move_public == 3) {
        a_score += 1;
      }

      if (b_move_public == 1 && a_move_public == 3) {
        b_score += 1;
      }
    }
  }

  val a_wins: bool{alice ⊓ bob} = a_score > b_score;
  output a_wins to alice;
  output a_wins to bob;
}
