host alice: {A & B<-}
host bob:   {B & A<-}

process main {
  val n: int{A ⊓ B} = 10;

  /* assume that input arrays are already sorted */
  val adata = Array[int]{A & B<-}(n);
  val bdata = Array[int]{B & A<-}(n);

  for (var i: int{A ⊓ B} = 0; i < n; i += 1) {
    adata[i] = input int from alice;
    bdata[i] = input int from bob;
  }

  var cur_a: int{A & B<-} = 0;
  var cur_b: int{B & A<-} = 0;
  for (var iter: int{A ⊓ B} = 0; iter < n; iter += 1) {
    if (declassify (adata[cur_a] <= bdata[cur_b]) to {A ⊓ B}) {
      cur_a += 1;

    } else {
      cur_b += 1;
    }
  }

  var median: int{A ⊓ B};
  if (declassify (adata[cur_a] <= bdata[cur_b]) to {A ⊓ B}) {
    median = declassify adata[cur_a] to {A ⊓ B};

  } else {
    median = declassify bdata[cur_b] to {A ⊓ B};
  }

  output median to alice;
  output median to bob;
}
