/* Alice an Bob bid on an item in two rounds.
 * Who had the higher bid is revealed after the first round,
 * but the bids are kept secret.
 * Person with the higher average bid wins.
 */
process main {
  /* Round 1 */
  val a1: int = endorse (input int from alice) to {A & B<-};
  val b1: int = endorse (input int from bob) to {B & A<-};

  /* Reveal who had the higher bid. */
  val a1_higher: bool = declassify (a1 > b1) to {A ⊓ B};
  output a1_higher to alice;
  output a1_higher to bob;

  /* Round 2 */
  val a2: int = endorse (input int from alice) to {A & B<-};
  val b2: int = endorse (input int from bob) to {B & A<-};

  /* Reveal the overall winner. */
  val a_higher: bool = declassify (a1 + a2 > b1 + b2) to {A ⊓ B};
  output a_higher to alice;
  output a_higher to bob;
}

process Host(alice) {
  /* First bid. */
  send 10 to main;

  /* Adjust bid based on result of Round 1. */
  val a1_higher: bool = receive bool from main;
  if (a1_higher) {
    send 5 to main;
  } else {
    send 20 to main;
  }

  /* See who won. */
  val a_higher: bool = receive bool from main;
  assert a_higher;
}

process Host(bob) {
  /* First bid. */
  send 20 to main;

  /* Adjust bid based on result of Round 1. */
  val a1_higher: bool = receive bool from main;
  if (a1_higher) {
    send 80 to main;
  } else {
    send 5 to main;
  }

  /* See who won. */
  val a_higher: bool = receive bool from main;
  assert a_higher;
}

host alice : {A}
host bob   : {B}
