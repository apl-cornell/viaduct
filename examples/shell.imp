process main {
  int{C} cin;
  int{A} ain;
  int{C & A<-} shell;
  int{A & C<-} guess;
  int{(A | C)-> & (A & C)<-} win;

  cin <- recv chuck_main;
  ain <- recv alice_main;

  /* Chuck and Alice must endorse their inputs to `shell` and `guess`, respectively. */
  shell = endorse(cin, {C & (A<-)});
  guess = endorse(ain, {A & (C<-)});

  /* Chuck must prove that `shell` is valid without revealing its value. */
  int{A & C} win_secret;
  bool{A && C} guard = declassify(1 <= shell && shell <= 3, {A && C});
  if (guard) {
    if (shell == guess) {
      win_secret = 1;

    } else {
      win_secret = 0;
    }

  } else {
      win_secret = -1;
  }

  win = declassify(win_secret, {(A | C)-> & (A & C)<-});
}

process chuck_main {
  send 1 to main;
}

process alice_main {
  send 2 to main;
}

/* Declare the actual machines (i.e. hosts) we have. */
host alice : {A};
host chuck : {C};
