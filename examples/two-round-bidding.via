/* Alice an Bob bid on an item in two rounds.
 * Who had the higher bid is revealed after the first round,
 * but the bids are kept secret.
 * Person with the higher average bid wins.
 */
process main {
  /* Round 1 */
  int a1 = endorse (input int from alice) to {A & B<-};
  int b1 = endorse (input int from bob) to {B & A<-};

  /* Reveal who had the higher bid. */
  bool a1_higher = declassify (a1 > b1) to {A ⊓ B};
  output a1_higher to alice;
  output a1_higher to bob;

  /* Round 2 */
  int a2 = endorse (input int from alice) to {A & B<-};
  int b2 = endorse (input int from bob) to {B & A<-};

  /* Reveal the overall winner. */
  bool a_higher = declassify (a1 + a2 > b1 + b2) to {A ⊓ B};
  output a_higher to alice;
  output a_higher to bob;
}

process Host(alice) {
  /* First bid. */
  send 10 to main;

  /* Adjust bid based on result of Round 1. */
  bool a1_higher = recv bool from main;
  if (a1_higher) {
    send 5 to main;
  } else {
    send 20 to main;
  }

  /* See who won. */
  bool a_higher = recv bool from main;
  assert a_higher;
}

process Host(bob) {
  /* First bid. */
  send 20 to main;

  /* Adjust bid based on result of Round 1. */
  bool a1_higher = recv bool from main;
  if (a1_higher) {
    send 80 to main;
  } else {
    send 5 to main;
  }

  /* See who won. */
  bool a_higher = recv bool from main;
  assert a_higher;
}

host alice : {A}
host bob   : {B}
